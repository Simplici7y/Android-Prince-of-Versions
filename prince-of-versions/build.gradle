apply plugin: 'com.android.library'
apply from: './quality.gradle'
apply plugin: 'com.novoda.bintray-release'

ext {
    publishedGroupId = 'co.infinum'
    publishedArtifactName = 'prince-of-versions'
}

android {
    compileSdkVersion buildVersions.compile
    buildToolsVersion buildVersions.tools

    defaultConfig {
        minSdkVersion buildVersions.min
        targetSdkVersion buildVersions.target
        versionCode 1
        versionName versions.prince
    }

    buildTypes {
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
        }
    }

    resourcePrefix 'prince_of_versions_'
}

dependencies {
    implementation fileTree(dir: 'libs', include: ['*.jar'])
    implementation deps.semver
    compileOnly deps.appcompat
    compileOnly deps.fbannotations

    testImplementation deps.junit
    testImplementation deps.mockito
    testImplementation deps.mockwebserver
    testImplementation deps.assertj
    testImplementation deps.json
    testCompileOnly deps.fbannotations
}

group = publishedGroupId
version = versions.prince

// https://github.com/novoda/bintray-release/wiki/Configuration-of-the-publish-closure
publish {
    userOrg = 'infinum'
    groupId = publishedGroupId
    artifactId = publishedArtifactName
    publishVersion = versions.prince
    desc = 'Library checks for application updates using configuration from some resource.'
    website = 'https://github.com/infinum/Android-prince-of-versions'
    licences = ["Apache-2.0"]
    repoName = 'android'
}

task sourcesJar(type: Jar) {
    from android.sourceSets.main.java.srcDirs
    classifier = 'sources'
}

artifacts {
    archives sourcesJar
}

apply from: '../tasks.gradle'

preBuild.dependsOn ':prince-of-versions:generateReadme'

