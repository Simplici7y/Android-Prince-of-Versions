apply plugin: 'com.android.library'
apply plugin: 'com.novoda.bintray-release'
apply from: './quality.gradle'

ext {
    publishedGroupId = 'co.infinum'
    publishedArtifactName = 'queen-of-versions'
}

android {
    compileSdkVersion buildVersions.compile
    buildToolsVersion buildVersions.tools

    defaultConfig {
        minSdkVersion buildVersions.min
        targetSdkVersion buildVersions.target
        versionCode 1
        versionName versions.queen
    }

    buildTypes {
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
        }
    }
}

dependencies {
    api project(path: ':prince-of-versions')
    implementation 'com.google.android.play:core:1.7.1'
    compileOnly deps.fbannotations
    compileOnly deps.appcompat

    testImplementation deps.junit
    testImplementation deps.mockito
    testImplementation deps.mockwebserver
    testImplementation deps.assertj
    testImplementation deps.json
    testCompileOnly deps.fbannotations
}

apply from: '../tasks.gradle'

group = publishedGroupId
version = versions.queen

// https://github.com/novoda/bintray-release/wiki/Configuration-of-the-publish-closure
publish {
    userOrg = 'infinum'
    groupId = publishedGroupId
    artifactId = publishedArtifactName
    publishVersion = versions.queen
    desc = 'Library checks for application updates using In-App updates.'
    website = 'https://github.com/infinum/Android-prince-of-versions'
    licences = ["Apache-2.0"]
    repoName = 'android'
}

task sourcesJar(type: Jar) {
    from android.sourceSets.main.java.srcDirs
    classifier = 'sources'
}

artifacts {
    archives sourcesJar
    archives javadocsJar
}

preBuild.dependsOn ':prince-of-versions:generateReadme'
preBuild.dependsOn ':prince-of-versions:generateReadmeQueen'

sourceCompatibility = "7"
targetCompatibility = "7"